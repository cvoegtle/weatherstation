buildscript {    // Configuration for building
    repositories {
        jcenter()    // Bintray's repository - a fast Maven Central mirror & more
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
    }
}

repositories {   // repositories for Jar's you access in your code
    maven {
        url 'https://maven-central.storage.googleapis.com'             // Google's mirror of Maven Central
//   url 'https://oss.sonatype.org/content/repositories/snapshots' // SNAPSHOT Repository (if needed)
    }
    jcenter()
    mavenCentral()
}

apply plugin: 'java'                              // standard Java tasks
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks


dependencies {
    providedCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile 'com.google.appengine:appengine:+'
    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile 'com.google.appengine:appengine-api-labs:+'
    compile 'com.google.appengine:appengine-endpoints:+'
    compile 'com.google.appengine:appengine-endpoints-deps:+'
    compile 'com.google.appengine:appengine-jsr107cache:+'
    compile 'net.sf.jsr107cache:jsr107cache:1.1'
    compile 'org.ow2.asm:asm:5.0.4'
    compile 'org.datanucleus:datanucleus-enhancer:3.1.1'
    compile 'org.datanucleus:datanucleus-api-jdo:3.1.3'
    compile 'org.datanucleus:datanucleus-api-jpa:3.1.3'
    compile 'com.google.appengine.orm:datanucleus-appengine:2.1.2'
    compile 'org.datanucleus:datanucleus-core:3.1.3'

    compile 'org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.0'
    compile 'javax.jdo:jdo-api:3.0'
    compile 'javax.transaction:jta:1.1'
}

// [START model]
appengine {  // App Engine tasks configuration
    run {      // local (dev_appserver) configuration (standard environments only)
        port = 8080                 // default
    }


    deploy {   // deploy configuration
        version = '52'
        project = 'instantwetter'
        stopPreviousVersion = false
        promote = false
    }
}

task datanucleusEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.output.classesDir)

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
                name : 'datanucleusenhancer',
                classpath : sourceSets.main.runtimeClasspath.asPath,
                classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
                // the below is for DataNucleus Enhancer 3.1.1
                // classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
                classpath: sourceSets.main.runtimeClasspath.asPath,
                verbose: true,
                api: 'JPA') {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }

    }
}

classes.dependsOn(datanucleusEnhance)
// [END model]

group = 'org.voegtle.weatherstation'   // Generated output GroupId
version = '52'          // Version in generated output

sourceCompatibility = 1.7  // App Engine Standard uses Java 7
targetCompatibility = 1.7 // App Engine Standard uses Java 7

/**
 <orderEntry type="library" name="com.google.appengine:appengine-jsr107cache:1.9.53" level="project" />
 <orderEntry type="library" name="net.sf.jsr107cache:jsr107cache:1.1" level="project" />
 <orderEntry type="library" name="com.google.appengine:appengine-api-1.0-sdk:1.9.53" level="project" />
 <orderEntry type="library" name="com.google.appengine:appengine-api-labs:1.9.53" level="project" />
 <orderEntry type="library" name="com.google.appengine.orm:datanucleus-appengine:1.0.10" level="project" />
 <orderEntry type="library" scope="RUNTIME" name="org.datanucleus:datanucleus-jpa:1.1.5" level="project" />
 <orderEntry type="library" name="org.apache.geronimo.specs:geronimo-jpa_3.0_spec:1.1.1" level="project" />
 <orderEntry type="library" name="org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1" level="project" />
 <orderEntry type="library" name="javax.jdo:jdo2-api:2.3-eb" level="project" />
 <orderEntry type="library" name="com.google.appengine:appengine-api-1.0-sdk:Release" level="project" />
 <orderEntry type="library" name="com.google.appengine:appengine-endpoints:1.9.53" level="project" />
 <orderEntry type="library" name="com.google.appengine:appengine-endpoints-deps:1.9.53" level="project" />
 <orderEntry type="library" name="javax.servlet:servlet-api:2.5" level="project" />
 <orderEntry type="library" scope="RUNTIME" name="org.datanucleus:datanucleus-core:1.1.5" level="project" />
 */