buildscript {    // Configuration for building
    repositories {
        jcenter()    // Bintray's repository - a fast Maven Central mirror & more
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.60"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

repositories {   // repositories for Jar's you access in your code
    maven {
        url 'https://maven-central.storage.googleapis.com'             // Google's mirror of Maven Central
//   url 'https://oss.sonatype.org/content/repositories/snapshots' // SNAPSHOT Repository (if needed)
    }
    jcenter()
    mavenCentral()
}

apply plugin: 'java'                              // standard Java tasks
apply plugin: 'kotlin'
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks
apply plugin: 'org.junit.platform.gradle.plugin'

dependencies {
    providedCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile 'com.google.appengine:appengine:+'
    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile 'com.google.appengine:appengine-api-labs:+'
    compile 'com.google.appengine:appengine-endpoints:+'
    compile 'com.google.appengine:appengine-endpoints-deps:+'
    compile 'com.google.appengine:appengine-jsr107cache:+'
    compile 'net.sf.jsr107cache:jsr107cache:1.1'
    compile 'org.ow2.asm:asm:5.0.4'
    compile 'org.datanucleus:datanucleus-enhancer:3.1.1'
    compile 'org.datanucleus:datanucleus-api-jdo:3.1.3'
    compile 'org.datanucleus:datanucleus-api-jpa:3.1.3'
    compile 'com.google.appengine.orm:datanucleus-appengine:2.1.2'
    compile 'org.datanucleus:datanucleus-core:3.1.3'

    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.60"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.1.60"

    compile 'org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.0'
    compile 'javax.jdo:jdo-api:3.0'
    compile 'javax.transaction:jta:1.1'

    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.2")
    testCompile ("org.mockito:mockito-core:2.+" )
}

junitPlatform {
    // platformVersion '1.0.2'
    filters {
        engines {
            // include 'junit-jupiter', 'junit-vintage'
            // exclude 'custom-engine'
        }
        tags {
            // include 'fast'
            exclude 'slow'
        }
        // includeClassNamePattern '.*Test'
    }
    // enableStandardTestTask true
    // reportsDir file('build/test-results/junit-platform') // this is the default
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

// [START model]
appengine {  // App Engine tasks configuration
    run {      // local (dev_appserver) configuration (standard environments only)
        port = 8080                 // default
    }


    deploy {   // deploy configuration
        // baliwetter elbwetter forstwetter herzowetter instantwetter leo-wetter
        // oxenwetter shenzhenwetter tegelwetter testwetter wettercentral wetterimages
        version = '53'
        project = 'instantwetter'
        stopPreviousVersion = true
        promote = true
    }
}



task datanucleusEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.output.classesDir)

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
                name : 'datanucleusenhancer',
                classpath : sourceSets.main.runtimeClasspath.asPath,
                classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
                classpath: sourceSets.main.runtimeClasspath.asPath,
                verbose: true,
                api: 'JPA') {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }

    }
}

classes.dependsOn(datanucleusEnhance)
// [END model]

sourceCompatibility = 1.8  // App Engine Standard uses Java 7
targetCompatibility = 1.8 // App Engine Standard uses Java 7
